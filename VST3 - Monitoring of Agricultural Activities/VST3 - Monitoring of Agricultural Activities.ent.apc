{Application 'VST3_MONITORING_OF_AGRICULTURA' logic file generated by CSPro}
PROC GLOBAL
numeric currentLineNumber;
array string labels(100);
array string vil_codes(100);
array numeric codes(100);


PROC VST3_MONITORING_OF_AGRICULTUR_FF

PROC REGION_NAME

PREPROC
$= TOUPPER(GETLABEL(REGION_CODE,REGION_CODE));
NOINPUT;

POSTPROC
IF $ = "" THEN
	ERRMSG("Region Name should be filled");
	MOVE TO REGION_CODE ADVANCE;
ENDIF;
PROC DISTRICT_CODE

onfocus
// create dynamic value set for districts in selected Region
//using lookup file

if loadcase(DISTRICTSPERREGION_DICT,REGION_CODE) = 1 then

	// In the lookup file, DISTRICTSPERREGION_REC is a multiply occurring
	// record since there is more than one DISTRICT per REGION. We
	// need to iterate through all the DISTRICTS in the REGION
	// and load them to the value set.
	numeric x;
	do x = 1 while x <= count(DISTRICTSPERREGION_DICT.DISTRICTSPERREGION_REC)
		labels(x) = DPR_DISTRICT_NAME(x);
		codes(x) = DPR_DISTRICT_CODE(x);
	enddo;
	codes(x) = notappl;
	setvalueset(DISTRICT_CODE, codes, labels);
endif;
PROC DISTRICT_NAME

PREPROC
$= TOUPPER(GETLABEL(DISTRICT_CODE,DISTRICT_CODE));
NOINPUT;

POSTPROC
IF $ = "" THEN
	ERRMSG("District Name should be filled");
	MOVE TO DISTRICT_CODE ADVANCE;
ELSE
	IF REGION_CODE <> TONUMBER(EDIT("99",DISTRICT_CODE)[1:1]) THEN;
		ERRMSG("%s District is not in %s Region. Please verify and correct",STRIP(GETLABEL(DISTRICT_CODE,DISTRICT_CODE)),STRIP(GETLABEL(REGION_CODE,REGION_CODE)));
		MOVE TO DISTRICT_CODE ADVANCE;
	ENDIF;
ENDIF;
PROC WARD_CODE

onfocus
// create dynamic value set for wards in selected district
//using lookup file

if loadcase(WARDSPERDISTRICT_DICT,DISTRICT_CODE) = 1 then

	// In the lookup file, WARDSPERDISTRICT_REC is a multiply occurring
	// record since there is more than one DISTRICT per REGION. We
	// need to iterate through all the DISTRICTS in the REGION
	// and load them to the value set.
	numeric x;
	do x = 1 while x <= count(WARDSPERDISTRICT_DICT.WARDSPERDISTRICT_REC)
		labels(x) = WPD_WARD_NAME(x);
		codes(x) = WPD_WARD_CODE(x);
	enddo;
	codes(x) = notappl;
	setvalueset(WARD_CODE, codes, labels);
endif;
PROC WARD_NAME

PREPROC
$= TOUPPER(GETLABEL(WARD_CODE,WARD_CODE));
NOINPUT;

POSTPROC
IF $ = "" THEN
	ERRMSG("Ward Name should be filled");
	MOVE TO WARD_CODE ADVANCE;
ELSE;
	IF DISTRICT_CODE <> TONUMBER(EDIT("9999",WARD_CODE)[1:2]) THEN;
		ERRMSG("%s Ward is not in %s District. Please verify and correct",STRIP(GETLABEL(WARD_CODE,WARD_CODE)),STRIP(GETLABEL(DISTRICT_CODE,DISTRICT_CODE)));
		MOVE TO WARD_CODE ADVANCE;
	ENDIF;	
ENDIF;
PROC VILLAGE_CODE

onfocus
// create dynamic value set for villages in selected ward
//using lookup file

if loadcase(VILLAGESPERWARD_DICT,WARD_CODE) = 1 then

	// In the lookup file, VILLAGESPERWARD_REC is a multiply occurring
	// record since there is more than one DISTRICT per REGION. We
	// need to iterate through all the DISTRICTS in the REGION
	// and load them to the value set.
	numeric x;
	do x = 1 while x <= count(VILLAGESPERWARD_DICT.VILLAGESPERWARD_REC)
		labels(x) = VPW_VILLAGE_NAME(x);
		vil_codes(x) = VPW_VILLAGE_CODE(x);
	enddo;
	codes(x) = notappl;
	setvalueset(VILLAGE_CODE, vil_codes, labels);
endif;
PROC VILLAGE_NAME

PREPROC
$= TOUPPER(GETLABEL(VILLAGE_CODE,VILLAGE_CODE));
NOINPUT;

POSTPROC
IF $ = "" THEN
	ERRMSG("Village Name should be filled");
	MOVE TO VILLAGE_CODE ADVANCE;
ELSE;
	IF WARD_CODE <> TONUMBER(VILLAGE_CODE[1:4]) THEN;
		ERRMSG("%s Village is not in %s Ward. Please verify and correct",STRIP(GETLABEL(VILLAGE_CODE,VILLAGE_CODE)),STRIP(GETLABEL(WARD_CODE,WARD_CODE)));
		MOVE TO VILLAGE_CODE ADVANCE;
	ENDIF;	
ENDIF;
PROC OFFICER_CODE

PREPROC
$ = 98;
NOINPUT;
PROC OFFICER_NAME

POSTPROC
IF $ = "" THEN
	ERRMSG("Field Officer in-charge should be filled");
	REENTER;
ENDIF;
PROC DOE

POSTPROC
IF $ IN MISSING, NOTAPPL THEN
	ERRMSG("Date should be filled");
	REENTER;
ENDIF;
PROC SHEET

POSTPROC
IF $ IN 0, MISSING, NOTAPPL THEN
	ERRMSG("Sheet Number should be filled");
	REENTER;
ENDIF;
PROC ACTIVITIES000

NUMERIC i;
IF FIRST_NAME(1) = "" THEN
	ERRMSG("There must be at least 1 Monitoring Activity");
	MOVE TO SHEET ADVANCE;
ELSE;
	i = 1;
	IF TOTOCC(ACTIVITIES000) > 0 THEN
		WHILE i <= TOTOCC(ACTIVITIES000) DO
			IF FIRST_NAME(i) = "" THEN
				DELETE(ACTIVITIES000(i));
				BREAK;
			ELSE
				i = i+1;
			ENDIF;
		ENDDO;
	ENDIF;
ENDIF;
PROC FIRST_NAME

//store current ocurrence for displaying in CAPI question 
onfocus
currentLineNumber = curocc();

POSTPROC
IF $ = "" THEN
	ENDGROUP;
ENDIF;
PROC AREA

PREPROC

// app - opens a distance measurement application on the device
execsystem("app:com.cegmas.geoarea"); // open GEO Area App
PROC OTHER_REMARKS

PREPROC
NUMERIC i = 0;
NUMERIC j = 0;

WHILE i <= 5 DO
	IF REMARKS[i:1] = "E" THEN
		j = 1;
		BREAK;
	ENDIF;
    i = i + 1;
ENDDO;

IF j <> 1 THEN
	SKIP TO SIGNATURE;
ENDIF;

POSTPROC
IF $ = "" AND j = 1 THEN
	ERRMSG("Please specify the other remarks");
	REENTER;
ENDIF;
